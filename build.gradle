buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.2.1'
    }
}

group 'me.loki2302'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'application'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'

mainClassName = 'me.loki2302.App'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.4.0.RELEASE'
    testCompile 'com.github.spullara.mustache.java:compiler:0.9.1'
    testCompile 'com.google.guava:guava:19.0'
    testCompile 'com.thoughtworks.qdox:qdox:2.0-M4'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.4.0.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '1.4.0.RELEASE'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.4'
    gems 'rubygems:asciidoctor-diagram:1.5.0'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    systemProperty 'snippetsDir', snippetsDir
    outputs.dir snippetsDir
}

asciidoctor {
    sourceDir 'src/main/asciidoc'
    sources {
        include 'app.adoc'
    }
    attributes = [
            sourceDir: file('src'),
            snippetsDir: snippetsDir
    ]
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    dependsOn jrubyPrepare, test
}
